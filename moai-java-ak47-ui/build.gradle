buildscript {

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }

}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

apply plugin: 'org.springframework.boot'

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
        mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
    }
}

dependencies {
    implementation project(':moai-java-ak47-common')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.ldap:spring-ldap-core'
    implementation 'org.springframework.security:spring-security-ldap'
    implementation 'org.springframework:spring-tx'
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    implementation 'de.codecentric:spring-boot-admin-starter-client'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.github.mvysny.kaributesting:karibu-testing-v10'
}

springBoot {
    buildInfo()
}

bootJar {
    baseName 'moai-java-ak47-ui'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.3
            }
        }
    }
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/github/wgx731/ak47/Application*',
                    'com/github/wgx731/ak47/service/*',
                    'com/github/wgx731/ak47/security/*',
                    'com/github/wgx731/ak47/config/*',
                    'com/github/wgx731/ak47/vaadin/config/*',
            ])
        }))
    }
}
